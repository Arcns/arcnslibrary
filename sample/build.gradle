apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.example.arcns"
        minSdkVersion 21 //5.0
        targetSdkVersion 29
        versionCode 17
        versionName "1.1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }
    kotlinOptions { jvmTarget = 1.8 }
    buildTypes {
        def baseurl_official = '"http://14.116.254.82:8088/est/"'
        def baseurl_test = '"http://183.56.194.120:8088/est/"'
        def baseurl_jj = '"http://183.56.194.120:8084/est/"'
        def baseurl_cq = '"http://183.56.194.120:8086/est/"'
        all {
            buildConfigField 'String', 'BASEURL_OFFICIAL', baseurl_official
            buildConfigField 'String', 'BASEURL_TEST', baseurl_test
            buildConfigField 'String', 'BASEURL_JIUJIANG', baseurl_jj
            buildConfigField 'String', 'BASEURL_CHONGQING', baseurl_cq
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'BASEURL', baseurl_official
        }
        debug {
            buildConfigField 'String', 'BASEURL', baseurl_test
        }
    }
    dataBinding {
        // 启用databinding，与kotlin一起使用时，必须引入：apply plugin: 'kotlin-kapt'
        enabled = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.stdlib
    testImplementation deps.test.junit
    androidTestImplementation deps.test.androidx_junit
    androidTestImplementation deps.test.androidx_espresso
    implementation deps.androidx.appcompat
    implementation deps.androidx.ktx
    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'
    // 相对布局
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // 多 dex 文件配置
    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"
    // material
    implementation 'com.google.android.material:material:1.1.0'
    // Navigation Kotlin
    // 开启navigation需要配置三个地方：module.gradle页面顶部apply、module.gradle页面dependencies、project.gradle页面的classpath
    def nav_version = "2.1.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // ViewModel and LiveData
    def lifecycle_version = "2.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    // 弹出框
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:lifecycle:3.3.0'
    implementation 'com.afollestad.material-dialogs:datetime:3.3.0'
    // 图片加载库
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // OKHttp
    implementation "com.squareup.okhttp3:okhttp:4.4.0"
    implementation("com.squareup.okhttp3:logging-interceptor:4.4.0")
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.7.0'
    // recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // 圆角图片
    implementation 'com.makeramen:roundedimageview:2.3.0'
    // 事件传递
    implementation 'org.greenrobot:eventbus:3.1.1'
    // viewpager2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    // 列表上拉下拉刷新布局
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0'
    //1.0.5及以前版本的老用户升级需谨慎，API改动过 大
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0'  //没有使用特殊Header，可以不加这行

    // 权限检查
    implementation 'com.yanzhenjie:permission:2.0.3'
    // 图片压缩大小
    implementation 'id.zelory:compressor:2.1.0'
    // 蓝牙
    implementation 'com.clj.fastble:FastBleLib:2.3.4'

    //高德3D地图so及jar
    implementation 'com.amap.api:3dmap:7.4.0'
    //高德定位功能
    implementation 'com.amap.api:location:4.9.0'
    //高德搜索
    implementation 'com.amap.api:search:7.3.0'

//    implementation 'com.github.Arcns:arcnslibrary:0.0.8'
    implementation project(path: ':arcns-core')
    implementation project(path: ':arcns-map-gaode')
    implementation project(path: ':arcns-map-baidu')
}
